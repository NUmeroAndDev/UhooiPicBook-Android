plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
    id 'io.gitlab.arturbosch.detekt'
    id 'kotlin-kapt'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.theuhooi.uhooipicbook"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 4
        versionName "1.3.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("release.keystore")
            storePassword System.getenv("RELEASE_KEYSTORE_STORE_PASSWORD")
            keyAlias "uhooipicbook"
            keyPassword System.getenv("RELEASE_KEYSTORE_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    buildFeatures {
        compose true
        dataBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    def coil_version = '1.1.1'
    implementation "io.coil-kt:coil:$coil_version"
    implementation "io.coil-kt:coil-base:$coil_version"
    implementation "io.coil-kt:coil-gif:$coil_version"

    // Firebase
    implementation 'com.google.firebase:firebase-analytics-ktx:17.5.0'
    implementation 'com.google.firebase:firebase-crashlytics-ktx:17.2.2'
    implementation 'com.google.firebase:firebase-firestore-ktx:21.7.0'
    implementation 'com.google.firebase:firebase-messaging:20.3.0'
    implementation 'com.google.firebase:firebase-perf:19.0.9'

    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    def lifecycle_version = '2.2.0'
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:1.0.0-alpha01"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.3.0-beta01'
    implementation 'androidx.core:core-ktx:1.5.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation "androidx.activity:activity-compose:1.3.0-alpha02"
    implementation "androidx.hilt:hilt-navigation:1.0.0-alpha03"

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"

    implementation "androidx.navigation:navigation-compose:1.0.0-alpha07"

    implementation 'com.google.android.material:material:1.2.1'
    implementation "com.google.android.material:compose-theme-adapter:1.0.0-alpha12"

    implementation "com.google.dagger:hilt-android:$rootProject.hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$rootProject.hilt_version"

    implementation "com.google.android.gms:play-services-oss-licenses:17.0.0"

    implementation "dev.chrisbanes.accompanist:accompanist-coil:0.5.1"

    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'androidx.test:runner:1.3.0'
}

kapt {
    correctErrorTypes = true
}

detekt {
    failFast = true
    buildUponDefaultConfig = true
    config = files("$projectDir/config/detekt/detekt.yml")
    baseline = file("$projectDir/config/detekt/baseline.xml")
    reports {
        xml {
            enabled = true
            destination = file("build/reports/detekt/detekt.xml")
        }
        html {
            enabled = true
            destination = file("build/reports/detekt/detekt.html")
        }
        txt {
            enabled = true
            destination = file("build/reports/detekt/detekt.txt")
        }
    }
}
